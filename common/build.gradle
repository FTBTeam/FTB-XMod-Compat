architectury {
    common(rootProject.enabled_platforms.split(","))
}

loom {
    accessWidenerPath = file("src/main/resources/ftbxmodcompat.accesswidener")
}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury:${rootProject.architectury_version}"

    modApi("dev.ftb.mods:ftb-library:${rootProject.ftb_library_version}") { transitive false }
    modApi("dev.ftb.mods:ftb-quests:${rootProject.ftb_quests_version}")
    modApi("dev.ftb.mods:ftb-chunks:${rootProject.ftb_chunks_version}")
    modApi("dev.ftb.mods:ftb-ranks:${rootProject.ftb_ranks_version}")
    modApi("dev.ftb.mods:ftb-teams:${rootProject.ftb_teams_version}")

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}")
    modCompileOnly("mezz.jei:jei-${rootProject.minecraft_version}-common-api:${rootProject.jei_version}")
    modCompileOnly("dev.emi:emi-xplat-intermediary:${rootProject.emi_version}+${rootProject.minecraft_version}:api")

    compileOnly("net.luckperms:api:${rootProject.luckperms_api_version}")

    modImplementation "dev.latvian.mods:kubejs:${rootProject.kubejs_version}"
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.archives_base_name
            version ftbPublishing.mavenVersion
            from components.java
        }
    }

    repositories {
        if (ftbPublishing.ftbToken) {
            maven {
                url ftbPublishing.ftbURL
                credentials {
                    username = ftbPublishing.ftbUser
                    password = ftbPublishing.ftbToken
                }
            }
        }

        if (ftbPublishing.sapsToken) {
            maven {
                url ftbPublishing.sapsURL
                credentials {
                    username = ftbPublishing.sapsUser
                    password = ftbPublishing.sapsToken
                }
            }
        }
    }
}
